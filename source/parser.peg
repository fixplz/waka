{
  function unwrap(val) {
    if(val.length == 1) return val[0]
  }
  function atom(bind, expr, op) {
    var el =
      op == '+' ? { many: expr }
      : op == '*' ? { any: expr }
      : op == '?' ? { opt: expr }
      : expr
    if(bind) el.bind = bind
    return el
  }
}


Start =
  sp
  init:JSEmbed?
  rules:Rule+
  { rules: rules, init: init, }
;

Rule =
  name:RuleHead
  eq
  %anc
  def:Expr
  semi
  { name: name, def: def }
;

RuleHead = n:word sp { n } ;

Expr =
  alt:( expr:ExprAction %sep slash )+
  { unwrap(alt) || {alt: alt} }
;

ExprAction =
  body:ExprSeq?
  format:JSEmbed?
  { format ? { format: format, of: body } : body }
;

ExprSeq =
  atoms:ExprAtom+
  { unwrap(atoms) || {seq: atoms} }
;

ExprAtom =
  bind:(n:word col {n})?
  expr:( Ref / Range / Literal / ExprParens )
  op:[?*+]? sp
  { atom(bind, expr, op) }
/ 
  op:[&!]
  expr:( Ref / Range / Literal / ExprParens ) sp
  { lookahead: expr, not: op == '!' }
/
  '%anc' sp { anchor: true }
;

ExprParens = '(' sp %anc e:InnerExpr ')' { e } ;

InnerExpr =
  expr:Expr
  ( tag:('%sep' / '%delim') sp token:Expr )?
  { tag ?
    { delim: expr, tag: tag, token: token }
    : expr
  }
;

Ref = n:word { ref: n } ;
Literal = s:str { str: s } ;

Range =
  '['
  %anc
  not:'^'? sp
  rs:(
    from:rangechar '-' to:rangechar sp { from: from, to: to }
  / chars:( (rangechar+) %delim sp )+ { oneof: chars.join('') }
  )*
  ']'
  { range: rs, not: not == '^' }
;

rangechar = strEsc / [^\[\]\ \^\n\r\u2028\u2029] ;

JSEmbed =
  isobj:( &( '{' sp word ' '* col ) {true} )?
  '{' sp
  embed:embed
  '}' sp
  { isobj ? '{' + embed + '}' : embed }
;

word = [a-z A-Z _][a-z A-Z 0-9 _]* ;

str =
  ["] x:(strEsc / [^"\n\r\u2028\u2029])* ["] { x }
/ ['] x:(strEsc / [^'\n\r\u2028\u2029])* ['] { x }
;
strEsc =
  '\\u' hex hex hex hex
/ '\\x' hex hex
/ '\\' any
;
hex = [0-9 a-f A-F] ;

embed =
  [^{}"']*
  (
    ( '{' embed '}' / str )
    [^{}"']*
  )* ;

eq = '=' sp ;
slash = '/' sp ;
semi = ';' sp ;
col = ':' sp ;

sp =
  (
    [\r\n\ ]+
  / '//' [^ \n]* '\n'
  / '/*' (!'*/' any)* '*/'
  )*
;

any = { _P.step(true) } ;

